plugins {
  id "com.gradleup.shadow" version "8.3.6"
  id "java"
}

java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

test {
  useJUnitPlatform()
}

ext.mainClass = "net.prominic.groovyls.GroovyLanguageServer"

tasks.build.dependsOn tasks.shadowJar

shadowJar {
  manifest {
    attributes "Main-Class": "net.prominic.groovyls.GroovyLanguageServer"
  }
}

repositories  {
  mavenCentral()
  maven { url "https://repo.gradle.org/gradle/libs-releases" }
}

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.24.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.24.0"
  implementation "org.apache.groovy:groovy:4.0.26"
  implementation "com.google.code.gson:gson:2.13.1"
  implementation "io.github.classgraph:classgraph:4.8.179"
  implementation "org.gradle:gradle-tooling-api:9.3.0"
  // Test framework support - needed for annotation detection
  compileOnly "org.junit.jupiter:junit-jupiter-api:5.11.4"
  compileOnly "org.testng:testng:7.10.2"
  compileOnly "org.spockframework:spock-core:2.4-M4-groovy-4.0"
  testImplementation platform("org.junit:junit-bom:5.11.4")
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.11.4"
}

// assuming that Moonshine-IDE is checked out in a folder that has the same
// parent folder as groovy-language-server, copies the built .jar file to the
// appropriate location
task deployMoonshine(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineSharedCore/src/elements/groovy-language-server/"
}
task deployMoonshineTest(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineDESKTOPevolved/bin-debug/elements/groovy-language-server/"
}
