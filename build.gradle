plugins {
  id "com.gradleup.shadow" version "8.3.6"
  id "java"
}

java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:deprecation"
}

test {
  useJUnitPlatform()
  maxParallelForks = Math.max(1, (int) (Runtime.runtime.availableProcessors() / 2))
  forkEvery = 50
  minHeapSize = "256m"
  maxHeapSize = "1024m"
}

ext.mainClass = "net.prominic.groovyls.GroovyLanguageServer"

tasks.build.dependsOn tasks.shadowJar

shadowJar {
  manifest {
    attributes "Main-Class": "net.prominic.groovyls.GroovyLanguageServer"
  }
}

repositories  {
  mavenCentral()
  maven { url = "https://repo.gradle.org/gradle/libs-releases" }
}

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:0.24.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.24.0"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.debug:0.24.0"
  implementation "org.apache.groovy:groovy:4.0.26"
  implementation "com.google.code.gson:gson:2.13.1"
  implementation "io.github.classgraph:classgraph:4.8.184"
  implementation "org.gradle:gradle-tooling-api:9.3.0"
  testImplementation platform("org.junit:junit-bom:5.11.4")
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
  testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.11.4"
}

// assuming that Moonshine-IDE is checked out in a folder that has the same
// parent folder as groovy-language-server, copies the built .jar file to the
// appropriate location
task deployMoonshine(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineSharedCore/src/elements/groovy-language-server/"
}
task deployMoonshineTest(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineDESKTOPevolved/bin-debug/elements/groovy-language-server/"
}
